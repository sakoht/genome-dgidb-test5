package Genome::Model::Command::AddReads::PostprocessAlignments;

use strict;
use warnings;

use Data::Dumper;

use above "Genome";
use Command; 

UR::Object::Class->define(
    class_name => __PACKAGE__,
    is => 'Command',
    has => [
        model_id   =>  { is => 'Integer', 
                      doc => "Identifies the genome model to which we'll add the reads." },
    ],
    has_optional => [
        limit_regions =>  { is => 'String',
                            doc => 'Which regions should be kept during further analysis' },
        bsub    =>  { is => 'Boolean',
                      doc => 'Sub-commands should be submitted to bsub. Default is yes.',
                      default_value => 1 },
        bsub_queue => { is => 'String',
                      doc => 'Which bsub queue to use for sub-command jobs, default is "long"',
                       default_value => 'long'},
        bsub_args => { is => 'String',
                       doc => 'Additional arguments passed along to bsub (such as -o, for example)',
                       default_value => '' },
        test => { is => 'Boolean',
                  doc => 'Create run information in the database, but do not schedule any sub-commands',
                  is_optional => 1,
                  default_value => 0},
    ]
);

sub sub_command_sort_position { 3 }

sub help_brief {
    "postprocess any incremental alignments generated by a model"
}

sub help_synopsis {
    return <<"EOS"
genome-model postprocess-alignments --model-id 5
                    
EOS
}

sub help_detail {
    return <<"EOS"
This command launches all of the appropriate commands to postprocess alignments accumulated on a temporary basis

All of the sub-commands listed below will be executed on the model in succession.
EOS
}


our $GENOME_MODEL_BSUBBED_COMMAND = "genome-model";
our @CHILD_JOB_CLASSES = ('Genome::Model::Command::AddReads::MergeAlignments',
                          'Genome::Model::Command::AddReads::UpdateGenotype',
                          'Genome::Model::Command::AddReads::FindVariations');


sub execute {
    my $self = shift;

    $DB::single=1;
    
    my @sub_command_classes = @CHILD_JOB_CLASSES;

    my $model = Genome::Model->get($self->model_id);
    my @subreferences_names = grep {$_ ne "all_sequences" } $model->get_subreference_names(reference_extension=>'bfa');

    unless (@subreferences_names > 0) {
        @subreferences_names = ('all_sequences');
    }
    
   
    foreach my $ref (@subreferences_names) { 
        my $last_bsub_job_id;
        foreach my $command_class ( @sub_command_classes ) {
          my $command = $command_class->create(model_id => $self->model_id, ref_seq_id=>$ref);
 
          if ($self->bsub) {
             $last_bsub_job_id = $self->run_command_with_bsub($command,$last_bsub_job_id);
          } elsif (! $self->test) {
             $command->execute();
          }
         }
    }

    return 1; 
}



sub run_command_with_bsub {
    my($self,$command,$last_bsub_job_id) = @_;

    my $queue = $self->bsub_queue;
    my $bsub_args = $self->bsub_args;

    # In case the command to run on the blades is different than 'genome-model'
    my $cmd = $command->command_name;
    $cmd =~ s/^\S+/$GENOME_MODEL_BSUBBED_COMMAND/;

    my $model_id = $self->model_id;
    my $refseq_id = $command->ref_seq_id;

    my $cmdline;
    { no warnings 'uninitialized';
        $cmdline = "bsub -q $queue $bsub_args" .
                   ($last_bsub_job_id && " -w $last_bsub_job_id") .
                   " $cmd --model-id $model_id --ref-seq-id $refseq_id";
    }

    if ($self->test) {
        $command->status_message("Test mode, command not executed: $cmdline");
        $last_bsub_job_id = 'test';
    } else {
        $self->status_message("Running command: " . $cmdline);

        my $bsub_output = `$cmdline`;
        my $retval = $? >> 8;

        if ($retval) {
            $self->error_message("bsub returned a non-zero exit code ($retval), bailing out");
            return;
        }

        if ($bsub_output =~ m/Job <(\d+)>/) {
            $last_bsub_job_id = $1;

        } else {
            $self->error_message('Unable to parse bsub output, bailing out');
            $self->error_message("The output was: $bsub_output");
            return;
        }

    }

    return $last_bsub_job_id;
}



sub _get_sorted_command_classes{
    my $self = shift;
    my @in_classes = @_;

    # Determine what all the sub-commands are going to be
    my @sub_command_classes = sort { $a->sub_command_sort_position
                                     <=>
                                     $b->sub_command_sort_position
                                   } @in_classes;
    
    return \@sub_command_classes;
}

sub is_not_to_be_run_by_add_reads {
    return 0;
}

1;

